apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {

    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    packagingOptions {
        exclude '*.apk'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    signingConfigs {
        debug {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }

    defaultConfig {
//        applicationId APPLICATION_ID
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME

        multiDexEnabled true
        jackOptions {
            enabled true
        }
    }

    /*渠道配置*/
    productFlavors {
        prod {
            applicationId APPLICATION_ID
        }
    }

    productFlavors.all {

    }

    buildTypes {
        release {
            buildConfigField "String", "HOST", DEBUG_HOST
            buildConfigField("boolean", "IS_DEBUG", "false")
            //Zipalign优化
            zipAlignEnabled true

            //签名
            signingConfig signingConfigs.release

            applicationIdSuffix ".release"
            minifyEnabled true
            shrinkResources = false
            debuggable false
            //混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 自定义输出配置
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为pcbwx_v1.0_wandou.apk
                        def fileName = "${variant.buildType.name}-V${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

        debug {
            buildConfigField "String", "HOST", RELEASE_HOST
            buildConfigField("boolean", "IS_DEBUG", "true")

            versionNameSuffix "-debug"
            //签名
            signingConfig signingConfigs.debug

            applicationIdSuffix ".debug"
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
        }
    }

    /*忽略编译过程中的lint报错*/
    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':view')
    compile project(':api')
    compile project(':sql')
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.jakewharton:butterknife:8.8.1'
    /*控件注解框架*/
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // 崩溃日志捕获
    compile 'ch.acra:acra:4.9.2'
}
